' Gambas class file

Public Sub ButtonCrearBingo_Click()
  
  Dim nombretmp As String
  Dim bingotmp As Bingo
  'preguntar por datos del nuevo bingo...
  'nombre
  'descripcion
  
  nombretmp = InputBox(("Nombre:"), ("Nuevo bingo"), ("NuevoBingo") & Format$(Now, "yyyy_mm_dd"))
  
  If nombretmp = "" Then 
    'ha cancelado la operacion
    Return
  Endif
  
  bingotmp = New Bingo
  bingotmp.NombreFichero = nombretmp
  
  FormModificarBingos.bingoEditor = bingotmp 
  
  FormModificarBingos.ShowModal()
  
End

Public Sub definirGridviewFicheros()
  
  With GridViewFicherosDatos
    .header = 3
    .rows.count = 0
    .columns.count = 3
    .Columns[0].title = ("Nombre") '"Archivo Bingo"
    .Columns[1].title = ("Temática")
    .Columns[2].title = ("Descripción")
    .Columns[0].width = 200
    .Columns[1].width = 200
    .Columns[2].width = 300
    
    .font.size = 12
    .Mode = Select.single
    .ShowCursor = True
  End With
  
End

Public Sub ButtonSalir_Click()
  
  Me.Close()
  
End

Public Sub Form_Open()
  
  Me.Title = ("Utilidades")
  definirGridviewFicheros()
  recargarGridview()
  Me.Center()
  
End

Public Sub recargarGridview()
  
  Dim arDir As String[]
  Dim nombredir As String
  Dim b As Bingo
  Dim contador As Integer
  Dim BingosDisponibles As New Bingo[]
  'cargo los datos de las carpetas de bingo en el gridview.
  
  ardir = Dir(User.home & "/bingoedu/", "*", gb.Directory)
  
  For Each nombredir In ardir
    b = New Bingo
    b.cargadatos(User.home & "/bingoedu/" & nombredir, True)
    GridViewFicherosDatos.Rows.count += 1
    GridViewFicherosDatos[contador, 0].text = nombredir
    GridViewFicherosDatos[contador, 1].text = b.titulo
    GridViewFicherosDatos[contador, 2].text = b.descripcion
    contador += 1
    BingosDisponibles.add(b)
    GridViewFicherosDatos.Tag = BingosDisponibles
  Next
  
End

Public Sub ButtonEditarBingo_Click()
  
  Dim bingotmp As Bingo
  
  If GridViewFicherosDatos.Row = -1 Then
    Message.Info(("Debe seleccionar un bingo educativo."))
    Return
  Else
    
    bingotmp = New Bingo
    
    bingotmp.NombreFichero = GridViewFicherosDatos[GridViewFicherosDatos.Row, 0].Text
    bingotmp.cargadatos(bingotmp.NombreFichero, True) 'TRUE: los datos vienen de una carpeta en el disco duro,y no de un ".be"
    
    FormModificarBingos.bingoEditor = bingotmp
    
    FormModificarBingos.ShowModal()
    
  Endif
  
End

Public Sub ButtonRefrescarGridview_Click()
  
  GridViewFicherosDatos.Rows.count = 0
  
  recargarGridview()
  
End

Public Sub ButtonBorrado_Click()
  'borrar una carpeta de datos
  
  Dim nombre As String
  Dim res As Integer
  Dim arfile As String[]
  Dim NombreFichero As String
  
  If GridViewFicherosDatos.Row = -1 Then
    Message.Info(("Debe de seleccionar un  bingo educativo"))
    Return
  Else
    
    nombre = GridViewFicherosDatos[GridViewFicherosDatos.Row, 0].Text
    
    res = Message.Question(("Se va a borrar el bingo educativo " & nombre), ("Si, borrar"), ("No, cancelar"))
    
    If res = 1 Then
      'procedo a borrar archivos
      arfile = Dir(User.Home & "/bingoedu/" & nombre, "*", gb.file)
      For Each nombrefichero In arfile
        Kill User.Home & "/bingoedu/" & nombre & "/" & NombreFichero
      Next
      
      'procedo a borrar el directorio
      Rmdir User.Home & "/bingoedu/" & nombre
      
      'refresco datos del gridviews
      ButtonRefrescarGridview_Click()
      
    Endif
    
  Endif
  
End

Public Sub ButtonCrearCopia_Click()
  
  Dim bingotmp As Bingo
  
  If GridViewFicherosDatos.Row = -1 Then
    Message.Info(("Debe seleccionar un bingo educativo."))
    Return
  Else
    
    'creo bingo temporal, para cargar los datos, que estan en la carpeta
    
    bingotmp = New Bingo
    
    bingotmp.NombreFichero = GridViewFicherosDatos[GridViewFicherosDatos.Row, 0].Text
    bingotmp.cargadatos(bingotmp.NombreFichero, True) 'TRUE: los datos vienen de una c
    
    'guardo los datos de la carpeta en un archivo .be
    bingotmp.guardarBingo(False)
    Message.Info(("Se ha guardado en ") & "\n" & User.Home & bingotmp.NombreFichero & ".be")
  Endif
  
End

Public Sub ButtonAgregarBingo_Click()
  
  Dim bingotmp As Bingo
  
  Dialog.Filter = ["*.be", ("Archivos Bingo Educativo")]
  If Not Dialog.OpenFile() Then
    bingotmp = New Bingo
    
    bingotmp.cargadatos(Dialog.Path, False)
    
    ButtonRefrescarGridview_Click()
    
  Endif
  
End

Public Sub ButtomImprimirCarton_Click()
  
  Dim bingotmp As Bingo
  
  If GridViewFicherosDatos.Row = -1 Then
    Message.Info(("Debe de seleccionar un  bingo educativo"))
    Return
  Else
    
    bingotmp = New Bingo
    
    bingotmp.NombreFichero = GridViewFicherosDatos[GridViewFicherosDatos.Row, 0].Text
    bingotmp.cargadatos(bingotmp.NombreFichero, True) 'TRUE: los datos vienen de una carpeta en el disco duro,y no de un ".be"
    
    ModuleVarios.imprimir(bingotmp)
  Endif
  
End

Public Sub GridViewFicherosDatos_DblClick()
  
  ButtonEditarBingo_Click()
  Stop Event
  
End

Public Sub ButtomImprimirElementosBingo_Click()
  
  Dim c As New CreandoInformeBingo
  Dim bingotmp As Bingo
  
  If GridViewFicherosDatos.Row = -1 Then
    Message.Info(("Debe de seleccionar un  bingo educativo"))
    Return
  Else
    
    bingotmp = New Bingo
    
    bingotmp.NombreFichero = GridViewFicherosDatos[GridViewFicherosDatos.Row, 0].Text
    bingotmp.cargadatos(bingotmp.NombreFichero, True) 'TRUE: los datos vienen de una carpeta en el disco duro,y no de un ".be"
    
    c.imprimirBingo(bingotmp)
    
  Endif
  
End
